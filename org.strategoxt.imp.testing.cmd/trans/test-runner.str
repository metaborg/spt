module test-runner

imports
  libstratego-lib
  libstratego-aterm
  include/Spoofax-Testing
  editor-common.generated
  spoofax_testing
  check
  
signature constructors 
  True  : Boolean  
  False : Boolean

strategies
  find(s) =
  	abspath 
  ; string-replace(|"/./", "/") => base
  ; readdir
  ; filter(
      !$[[base]/[<id>]]
    ; if <isdir> <filemode> then
        find(s)
      else
        s
      end
    )
  ; flatten-list
  
  test-runner =
    testlistener-init 
  ; find-all-testsuites-in-project
  ; map(testlistener-add-testsuite(|<get-testsuite-name-from-file>,<id>))
  ; map(try(run-testsuite))
  ; !None()

  test-runner-file:
    (selected, position, ast, path, project-path) -> None()
    with
      <make-dynamic-rules> (ast, path, project-path)
    ; testlistener-init 
    ; !$[[project-path]/[path]]
    ; testlistener-add-testsuite(|<get-testsuite-name-from-file>,<id>)
    ; try(run-testsuite)
  
  find-all-testsuites-in-project =
    <find(has-extension(|"spt"))> "." 
  ; string-sort

  run-testsuite :
    file -> (ast, [])
    where 
      ast  := <parse-spt-file> file
    ; ast' := <topdown(repeat(spt-desugar)); alltd(spt-desugar-refactoring-conditions)> ast
    // ; (ast', _, _, _) := <editor-analyze>(ast, path, project-path)
    ; testcases := <collect-all(testlistener-add-testcase(|file, <test-to-description>,<origin-offset;Fst>))>ast'
    ; <map(run-testcase(|file))>testcases
  
  get-testsuite-name :
    testsuite(hds, _) -> name
    where name := <fetch-elem(?Name(<id>))>hds
  
  get-testsuite-name-from-file =
    parse-spt-file ; get-testsuite-name <+ id
    
  run-testcase(|testsuite) = 
    ?tst
  ; test-to-description => name
  ; testlistener-start-testcase(|testsuite, name)
  ; if errors := <check-error>tst ; not((id,[])) then
      testlistener-finish-testcase(|testsuite, name, False(), errors)
    else
      testlistener-finish-testcase(|testsuite, name, True(), []) 
    end
    
  test-to-description = 
    ?Test(_, description(<id>), _, _)  
  + ?TestStratego(_, description(<id>), _, _)  
  + ?TestEmpty(_, description(<id>))

  external testlistener-init(|)
  external testlistener-add-testsuite(|testsuite, filename)
  external testlistener-add-testcase(|testsuite, description, linenr)
  external testlistener-start-testcase(|testsuite, description)
  external testlistener-finish-testcase(|testsuite, description, result, messages)
  external parse-spt-file(|)
    
  