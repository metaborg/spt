buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.3.12"
    id "de.fuerstenau.buildconfig" version "1.1.8"
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'

group = projectGroup
version = projectVersion
sourceCompatibility = javaVersion
targetCompatibility = javaTargetVersion

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://artifacts.metaborg.org/content/repositories/releases/"
    }
    maven {
        url "https://artifacts.metaborg.org/content/repositories/snapshots/"
    }
    maven {
        url "https://raw.githubusercontent.com/pluto-build/pluto-build.github.io/master/mvnrepository/"
    }
    maven {
        url "http://sugar-lang.github.io/mvnrepository/"
    }
}

intellij {
    version intellijVersion
    pluginName projectName
    downloadSources Boolean.valueOf(intellijSources)
    sameSinceUntilBuild Boolean.valueOf(intellijEAP)
    alternativeIdePath intellijLocalPath
    // WORKAROUND: For Java 9/10, instrumentation fails with the error:
    //     Class not found: javax.swing.JPanel
    // https://github.com/JetBrains/gradle-intellij-plugin/issues/235
    instrumentCode false

    patchPluginXml {
        sinceBuild intellijSinceBuild
        untilBuild intellijUntilBuild
        pluginDescription projectDescription
        changeNotes projectChangeNotes
    }

    publishPlugin {
        username publishUsername
        password publishPassword
        channels publishChannel
    }
}

buildConfig {
    appName = project.name
    version = project.version

    clsName = 'BuildConfig'
    packageName = 'org.metaborg.spt.testrunner.intellij'
    charset = 'UTF-8'

    buildConfigField 'String', 'METABORG_VERSION', metaborgVersion
}

dependencies {
    compile     "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile     ("org.metaborg:org.metaborg.core:$metaborgVersion") {
        exclude group: "org.slf4j"
        exclude group: "com.google.guava"
    }
    compile     ("org.metaborg:org.metaborg.spoofax.core:$metaborgVersion") {
        exclude group: "org.slf4j"
        exclude group: "com.google.guava"
    }
    compile     ("org.metaborg:org.metaborg.spoofax.meta.core:$metaborgVersion") {
        exclude group: "org.slf4j"
        exclude group: "com.google.guava"
    }

    compile     "com.google.guava:guava:26.0-jre"
    compile     "org.slf4j:slf4j-api:1.7.10"
    compile     "com.google.code.findbugs:jsr305:3.0.0"
    compile     "org.apache.commons:commons-lang3:3.4"
    compile     "org.apache.commons:commons-vfs2:2.1"
    compile     "com.google.inject:guice:$guiceVersion"
    compile     "com.google.inject.extensions:guice-assistedinject:$guiceVersion"
    compile     "com.google.inject.extensions:guice-multibindings:$guiceVersion"
    compile     "org.javassist:javassist:3.20.0-GA"

    runtime     "org.metaborg:org.metaborg.meta.lang.spt:$metaborgVersion@spoofax-language"
    runtime     "org.metaborg:org.metaborg.spt.cmd:$metaborgVersion"

    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
    testCompile "junit:junit:4.12"
}
